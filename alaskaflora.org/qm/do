#!/usr/bin/gawk -f 

BEGIN{
  
  # READ QUERY STRING
  split(ENVIRON["QUERY_STRING"], qs, "&")
  for (q in qs) {
    split(qs[q], qp, "=")
    f[qp[1]] = substr(urldecode(qp[2]),1,100)
  }

  # SEARCH
  if ((f["method"] == "search") && (f["q"])) {
    
    header("Arctos quick map: Search results")
    print "<h1>Search results</h1>"
    print "<p>Query = <i>" f["q"] "</i></p>"

    # Read data
    PROCINFO["sorted_in"] = "@ind_str_asc"

    FS="|"
    while ((getline < "data") > 0) {
      if ((tolower($2) ~ tolower(f["q"]) ) ||   \
          (tolower($3) ~ tolower(f["q"]) )) {   \
        # Arctos
        if ($6 == "a") {
          fam[$3] = $2
          num[$3]++
        }
        # Hulten
        else if ($6 == "h")
          numh[$3] = "<a href=\"https://alaskaflora.org/hulten/do"\
            "?method=search&amp;q=" $3 "\" target=\"_blank\">yes</a>"
        # iNat (could add link, if I redownload data with taxon_id)
        else if ($6 == "i")
          numi[$3] = "yes"
      }
    }
    
    if (length(fam)) {
      print "<table>"
      print "<tr><th>Name</th><th>Family</th><th>Arctos<br/>records</th>"\
        "<th>In Hultén</th><th>In iNat</th></tr>"
      for (i in fam)
        print "<tr><td><a href=\"do?method=detail&amp;taxon=" i "\">" i "</a>" \
          "</td><td>" fam[i] "</td><td>" num[i] "</td><td>" numh[i]     \
          "</td><td>" numi[i] "</td></tr>"
      print "</table>"
    }
    else
      print "<p>No results</p>"

    print "<p>[ <a href=\"do\">Home</a> ]</p>"
    footer()
  }

  # DETAILS
  else if ((f["method"] == "detail") && f["taxon"]) {
    
    FS="|"
    while ((getline < "data") > 0)
      if ($3 == f["taxon"]) {
        gsub(/UAM:Herb:/,"",$1)
        gsub(/^i/,"",$1)
        # generate points
        if ($6 == "a") 
          outxy = outxy                                                 \
            " L.circle([" $5 ", " $4 "], attr).bindPopup('"             \
            "<a href=\"https://arctos.database.museum/guid/UAM:Herb:"   \
            $1 "\" target=\"_blank\">Arctos: UAM:Herb:" $1 "</a>')"     \
            ".addTo(arctos) ;"
        if ($6 == "i") {
          outxyi = outxyi                                               \
            " L.circle([" $5 ", " $4 "], attr3).bindPopup('"            \
            "<a href=\"https://www.inaturalist.org/observations/"       \
            $1 "\" target=\"_blank\">iNat obs:" $1 "</a>')"             \
            ".addTo(inat) ;"
        }
        else if ($6 == "h") {
          outxyh = outxyh \
            "L.circle([" $5 ", " $4 "], attr2).addTo(hulten); "
        }
      }

    # HTML for page
    header("Arctos quick map: " f["taxon"])
    print "<p><b>" f["taxon"] "</b></p>"
    print "<div id=\"map\" style=\"height: 400px; width: 400px;\"></div>"

    print "<p style=\"font-size: 10px;\">Data licences: <a href=\"https://arctos.database.museum/collection/UAM:Herb\">Arctos ALA collections</a>, <a href=\"https://alaskaflora.org/hulten/\">Hultén's Flora</a>, <a href=\"https://www.inaturalist.org/pages/help#GBIFdata\">iNat</a></p>"
    
    print "<p>[ <a href=\"do\">Home</a> ]</p>"

    # Javascript
    print "<script type=\"text/javascript\">"
    # points, in layers, attributes first
    print "  var attr = { color: 'red', radius: 5000 };"
    print "  var attr2 = { color: 'blue', radius: 5000 };"
    print "  var attr3 = { color: 'yellow', radius: 5000 };"

    startLayers = "base"
    if (outxy) {
      startLayers = startLayers ", arctos"
      makeOverlays = "\"Arctos (red)\": arctos"
      print "var arctos = L.layerGroup();"
      print outxy
    }
    if (outxyi) {
      startLayers = startLayers ", inat"
      makeOverlays = makeOverlays ", \"iNat (yellow)\": inat" 
      print "var inat = L.layerGroup();"
      print outxyi
    }
    if (outxyh) {
      startLayers = startLayers ", hulten"
      makeOverlays = makeOverlays ", \"Hulten (blue)\": hulten"
      print "var hulten = L.layerGroup();"
      print outxyh
    }

    # base layer
    print " var base = L.tileLayer('https://{s}.tile.opentopomap.org/{z}/"\
      "{x}/{y}.png', "                                                  \
      "{ attribution: '<a href=\"https://www.openstreetmap.org/copyright\">" \
      "OSM</a>, <a href=\"http://viewfinderpanoramas.org\">SRTM</a>, "\
      "<a href=\"https://opentopomap.org\">OTM</a>' });"

    # make the map, with three overlay layers
    print "  var map = L.map('map', "\
      "{ zoomControl: false, center: [63, -150], zoom: 3, layers: [" \
      startLayers "] }) ;"
    print "var baseLayer = { \"Base Layer\": base };"
    print "var overlays = {" makeOverlays "};"
    print "L.control.layers(baseLayer, overlays, { hideSingleBase : true}).addTo(map);"
    print "</script>"

    footer()
    
  }
  
  else {

    header("Arctos quick map")
    
    print "<h1>Arctos quick maps</h1>"
    print "<form action=\"do\">"
    print "<table>"
    print "<tr><td>Search scientific name, family:</td><td></td></tr>" 
    print "<td><input type=\"hidden\" name=\"method\" value=\"search\"/>" \
      "<input type=\"text\" name=\"q\"/></td>"                          \
      "<td><input type=\"submit\" value=\"Submit\"/></td></tr></table>"
    print "</form>"
    
    footer()
    
  }
    
  exit 1
}

  function header(title) {
  # version history: [chars app] -> [tcm app] -> [hulten] -> here
  
  print "Content-type: text/html\n\n"                                   \
    "<!DOCTYPE html>"                                                   \
    "<html xmlns=\"http://www.w3.org/1999/xhtml\" "                     \
    "      prefix=\"og: https://ogp.me/ns#\">"                          \
    "<head><title>" title "</title>"                                    \
    "<meta http-equiv=\"Content-Type\" content=\"text/html;"            \
    "charset=utf-8\" />"                                                \
    "<link rel=\"icon\" type=\"image/png\" href=\"../img/favicon.png\"/>"
  print "<style type=\"text/css\">"                                     \
    "body { font-size: 14px; font-family: 'Montserrat', "               \
    "Verdana, Arial, Helvetica, sans-serif; }"                          \
    ".main {width: 1000px; padding-top: 10px; margin-left: auto;"       \
    "  margin-right: auto; }"                                           \
    "h1 { padding-top:20px; }"                                          \
    "select , option { font-size: 14px }"                               \
    "table { border-collapse: collapse }"                               \
    "td, th { border: 1px solid black; padding: 5px }"                  \
    "a { color:#15358d; text-decoration:none; border-bottom-style:none }" \
    "a:visited { color:#9f1dbc }"                                       \
    "a:hover {color:#15358d; border-bottom-style:solid; "               \
    "border-bottom-width:thin }"                                        \
    "</style>"                                                        
  
  print "<link rel=\"stylesheet\" href=\"https://unpkg.com/leaflet@1.7.1/dist/leaflet.css\" integrity=\"sha512-xodZBNTC5n17Xt2atTPuE1HxjVMSvLVW9ocqUKLsCC5CXdbqCmblAshOMAS6/keqq/sMZMZ19scR4PsZChSR7A==\" crossorigin=\"\"/>"
  print "<script src=\"https://unpkg.com/leaflet@1.7.1/dist/leaflet.js\" integrity=\"sha512-XQoYMqMTK8LvdxXYG3nZ448hOEQiglfqkJs1NOQV44cWnUrBc8PkAOcXy20w0vlaXaVUearIOBhiXZ5V3ynxwA==\" crossorigin=\"\"></script>"

  print "</head>\n<body>"
  print "<div class=\"main\">"


}

function footer() {
   
  print "</div>"
  print "</body>\n</html>";
}

function urldecode(text,   hex, i, hextab, decoded, len, c, c1, c2, code) {
# decode urlencoded string
# urldecode function from Heiner Steven
#   http://www.shelldorado.com/scripts/cmds/urldecode
# version 1
	
  split("0 1 2 3 4 5 6 7 8 9 a b c d e f", hex, " ")
  for (i=0; i<16; i++) hextab[hex[i+1]] = i
  
  decoded = ""
  i = 1
  len = length(text)
  
  while ( i <= len ) {
    c = substr (text, i, 1)
    if ( c == "%" ) {
      if ( i+2 <= len ) {
        c1 = tolower(substr(text, i+1, 1))
        c2 = tolower(substr(text, i+2, 1))
        if ( hextab [c1] != "" || hextab [c2] != "" ) {
          # print "Read: %" c1 c2;
          # Allow: 
          # 20 begins main chars, but dissallow 7F (wrong in orig code!)
          #   tab, newline, formfeed, carriage return
          if ( ( (c1 >= 2) && ((c1 c2) != "7f") )   \
               || (c1 == 0 && c2 ~ "[9acd]") )
            {
              code = 0 + hextab [c1] * 16 + hextab [c2] + 0
              # print "Code: " code
              c = sprintf ("%c", code)
            } else {
            # for dissallowed chars
            c = " "
          }
          i = i + 2
        }
      }
    } else if ( c == "+" ) 	# special handling: "+" means " "
      c = " "
    decoded = decoded c
    ++i
  }
  
  # change linebreaks to \n
  gsub(/\r\n/, "\n", decoded);
  # remove last linebreak
  gsub(/[\n\r]*$/,"",decoded);
  return decoded
}

