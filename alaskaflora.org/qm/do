#!/usr/bin/gawk -f 

BEGIN{
  
  # READ QUERY STRING
  split(ENVIRON["QUERY_STRING"], qs, "&")
  for (q in qs) {
    split(qs[q], qp, "=")
    f[qp[1]] = substr(urldecode(qp[2]),1,100)
  }

  # SEARCH
  if ((f["method"] == "search") && (f["q"])) {
    
    header("Arctos quick map: Search results")
    print "<h1>Search results</h1>"
    print "<p>Query = <i>" f["q"] "</i></p>"

    # Read data
    PROCINFO["sorted_in"] = "@ind_str_asc"

    FS="|"
    while ((getline < "data") > 0) {
      if ((tolower($2) ~ tolower(f["q"]) ) ||   \
          (tolower($3) ~ tolower(f["q"]) )) {   \
        if ($6 == "a") {
          fam[$3] = $2
          num[$3]++
        }
        else if ($6 == "h")
          numh[$3] = "<a href=\"https://alaskaflora.org/hulten/do?method=search&q=" $3 "\" target=\"_blank\">yes</a>"
      }
    }
    if (length(fam)) {
      if (f["hulten"] == "y") {
        print "<table>"
        print "<tr><th>Name</th><th>Family</th><th>Arctos<br/>records</th><th>In Hultén</th></tr>"
        for (i in fam)
          print "<tr><td><a href=\"do?method=detail&amp;hulten=y&amp;taxon=" i "\">" i "</a></td><td>" fam[i] "</td><td>" num[i] "</td><td>" numh[i] "</td></tr>"
        print "</table>"
      }
      else {
        print "<table>"
        print "<tr><th>Name</th><th>Family</th><th>Arctos<br/>records</th></tr>"
        for (i in fam)
          print "<tr><td><a href=\"do?method=detail&amp;taxon=" i "\">" i "</a></td><td>" fam[i] "</td><td>" num[i] "</td></tr>"
        print "</table>"
      }
    }
    else
      print "<p>No results</p>"

    print "<p>[ <a href=\"do\">Home</a> ]</p>"
    footer()
  }

  # DETAILS
  else if ((f["method"] == "detail") && f["taxon"]) {

    FS="|"
    while ((getline < "data") > 0)
      if ($3 == f["taxon"]) {
        gsub(/UAM:Herb:/,"",$1)
        if ($6 == "a") 
          outxy = outxy "var p" $1                                  \
            " = L.circle([" $5 ", " $4 "], attr).addTo(map); "      \
            "p" $1 ".bindPopup('"                                       \
            "<a href=\"https://arctos.database.museum/guid/UAM:Herb:"   \
            $1 "\" target=\"_blank\">UAM:Herb:" $1 "</a>'); "
        else {
          hulten = 1
          outxyh = outxyh "L.circle([" $5 ", " $4 "], attr2).addTo(map); "
        }
      }
    
    header("Arctos quick map: " f["taxon"])
    print "<p><b>" f["taxon"] "</b></p>"
    print "<div id=\"map\" style=\"height: 400px; width: 400px;\"></div>"

    if ((f["hulten"] == "y") && hulten) 
      print "<p style=\"font-size: 10px;\">(Red points: Arctos collections [clickable]; Blue points: dots on <a href=\"https://alaskaflora.org/hulten/do?method=search&q=" f["taxon"] "\" target=\"_blank\">Hultén maps</a>)</p>"
    else 
      print "<p style=\"font-size: 10px;\">(Red points: Arctos collections [clickable])</p>"
    print "<p>[ <a href=\"do\">Home</a> ]</p>"
    
    print "<script type=\"text/javascript\">"
    print "  var map = L.map('map', { zoomControl: false }).setView([63, -150], 3);"
    print "  L.tileLayer('https://{s}.tile.opentopomap.org/{z}/{x}/{y}.png', { attribution: '<a href=\"https://www.openstreetmap.org/copyright\">OSM</a>, <a href=\"http://viewfinderpanoramas.org\">SRTM</a>, <a href=\"https://opentopomap.org\">OTM</a>' }).addTo(map);"
    print "  var attr = { color: 'red', radius: 10000 };"
    print "  var attr2 = { color: 'blue', radius: 5000 };"
    if (outxy)
      print outxy
    if (outxyh && (f["hulten"] == "y"))
      print outxyh
    print "</script>"

    footer()
    
  }
  
  else {

    header("Arctos quick map")
    
    print "<h1>Arctos quick maps</h1>"
    print "<form action=\"do\">"
    print "<table>"
    print "<tr><td>Search scientific name, family:</td>"    \
      "<td>Show Hultén points</td><td></td></tr>"
    print "<tr><td><input type=\"hidden\" name=\"method\" value=\"search\"/>" \
      "<input type=\"text\" name=\"q\"/></td>"    \
      "<td><select name=\"hulten\"/>"             \
      "<option value=\"n\" selected=\"selected\">No</option>"           \
      "<option value=\"y\">Yes</option></select></td><td>"              \
      "<input type=\"submit\" value=\"Submit\"/></td></tr></table>"
    print "</form>"

    footer()
    
  }
    
  exit 1
}

function header(title) {
  # version history: [chars app] -> [tcm app] -> [hulten] -> here
  
  print "Content-type: text/html\n\n"                                   \
    "<!DOCTYPE html>"                                                   \
    "<html xmlns=\"http://www.w3.org/1999/xhtml\" "                     \
    "      prefix=\"og: https://ogp.me/ns#\">"                          \
    "<head><title>" title "</title>"                                    \
    "<meta http-equiv=\"Content-Type\" content=\"text/html;"            \
    "charset=utf-8\" />"                                                \
    "<link rel=\"icon\" type=\"image/png\" href=\"../img/favicon.png\"/>"
  print "<style type=\"text/css\">"                                     \
    "body { font-size: 14px; font-family: 'Montserrat', "               \
    "Verdana, Arial, Helvetica, sans-serif; }"                          \
    ".main {width: 1000px; padding-top: 10px; margin-left: auto;"       \
    "  margin-right: auto; }"                                           \
    "h1 { padding-top:20px; }"                                          \
    "select , option { font-size: 14px }"                               \
    "table { border-collapse: collapse }"                               \
    "td, th { border: 1px solid black; padding: 5px }"                  \
    "a { color:#15358d; text-decoration:none; border-bottom-style:none }" \
    "a:visited { color:#9f1dbc }"                                       \
    "a:hover {color:#15358d; border-bottom-style:solid; "               \
    "border-bottom-width:thin }"                                        \
    "</style>"                                                        
  
  print "<link rel=\"stylesheet\" href=\"https://unpkg.com/leaflet@1.7.1/dist/leaflet.css\" integrity=\"sha512-xodZBNTC5n17Xt2atTPuE1HxjVMSvLVW9ocqUKLsCC5CXdbqCmblAshOMAS6/keqq/sMZMZ19scR4PsZChSR7A==\" crossorigin=\"\"/>"
  print "<script src=\"https://unpkg.com/leaflet@1.7.1/dist/leaflet.js\" integrity=\"sha512-XQoYMqMTK8LvdxXYG3nZ448hOEQiglfqkJs1NOQV44cWnUrBc8PkAOcXy20w0vlaXaVUearIOBhiXZ5V3ynxwA==\" crossorigin=\"\"></script>"

  print "</head>\n<body>"
  print "<div class=\"main\">"


}

function footer() {
   
  print "</div>"
  print "</body>\n</html>";
}

function urldecode(text,   hex, i, hextab, decoded, len, c, c1, c2, code) {
# decode urlencoded string
# urldecode function from Heiner Steven
#   http://www.shelldorado.com/scripts/cmds/urldecode
# version 1
	
  split("0 1 2 3 4 5 6 7 8 9 a b c d e f", hex, " ")
  for (i=0; i<16; i++) hextab[hex[i+1]] = i
  
  decoded = ""
  i = 1
  len = length(text)
  
  while ( i <= len ) {
    c = substr (text, i, 1)
    if ( c == "%" ) {
      if ( i+2 <= len ) {
        c1 = tolower(substr(text, i+1, 1))
        c2 = tolower(substr(text, i+2, 1))
        if ( hextab [c1] != "" || hextab [c2] != "" ) {
          # print "Read: %" c1 c2;
          # Allow: 
          # 20 begins main chars, but dissallow 7F (wrong in orig code!)
          #   tab, newline, formfeed, carriage return
          if ( ( (c1 >= 2) && ((c1 c2) != "7f") )   \
               || (c1 == 0 && c2 ~ "[9acd]") )
            {
              code = 0 + hextab [c1] * 16 + hextab [c2] + 0
              # print "Code: " code
              c = sprintf ("%c", code)
            } else {
            # for dissallowed chars
            c = " "
          }
          i = i + 2
        }
      }
    } else if ( c == "+" ) 	# special handling: "+" means " "
      c = " "
    decoded = decoded c
    ++i
  }
  
  # change linebreaks to \n
  gsub(/\r\n/, "\n", decoded);
  # remove last linebreak
  gsub(/[\n\r]*$/,"",decoded);
  return decoded
}

