#!/bin/gawk -f

# A simple webapp for managing sequence sampling 
# (c) Cam Webb <cw@camwebb.info>, 2020

# INSTALLATION
#
# 1. If using Apache httpd, install this file in a cgi executable
#    location and create a .htaccess file, minimally: 
#        Options +ExecCGI -Indexes
#        <Files "do">
#          SetHandler cgi-script
#        </Files>
#        DirectoryIndex do
#    Optionally add:
#        <FilesMatch "^\.pw$">
#          Deny from all
#        </FilesMatch>
#        AuthType Basic
#        AuthName "Password Protected"
#        AuthUserFile /full/path/to/.pw
#        Require valid-user
#
# 2. Install function libraries from https://github.com/camwebb/awk-lib
#    and include, using full path

@include "/usr/local/share/awk/db_sql.awk"
@include "/usr/local/share/awk/util_http.awk"
@include "/usr/local/share/awk/db_secrets.awk"

# 3. On and available MySQL or MariaDB DB, install the empty DB at
#    http://alaskaflora.org/chars/chars.sql and modify as needed. Make sure
#    the access info is in the included db_secrets.awk

BEGIN{
  PROCINFO["sorted_in"] = "@ind_str_asc"
  
  db_secrets()
 
  # READ QUERY STRING
  split(ENVIRON["QUERY_STRING"], qs, "&");
  for (i in qs) {
    split(qs[i], qp, "=");
    f[qp[1]] = substr(urldecode(qp[2]),1,2000); 
  }
  
  # DETERMINE AND EXECUTE METHOD
  seeall() ;
  
}

function seeall(       error, actiontext, i, ts, taxsum) {

  header("Samples")

  # Block 1: title
  print "<h2>Sample-barcode DB</h2>"

  # preactions
  if (f["preaction"] == "delete") {
    error = db_sql("seq1", "DELETE FROM sample WHERE sample_no = " f["del"] \
                   ";")
    if (error) actiontext = "<span style=\"color:red;\">UPDATE FAILURE</span>"
    else actiontext = "<span style=\"color:green;\">Sample " f["del"] " deleted</span>"
  }
  
  else if (f["preaction"] == "add") {
    # check barcode
    gsub(/[[:space:]]/,"",f["barcode"])
    db_query("seq1", "SELECT id , alaac, taxon FROM ala WHERE barcode = '" \
             f["barcode"] "';")
    if (!DBQ[1,"id"])
      actiontext = "<span style=\"color:red;\">Barcode " f["barcode"]  \
        " not found</span>"
    else {
      if (f["notes"]) f["notes"] = "'" \
                        gensub(/(['"])/,"\\\\\\1","G",f["notes"]) "'"
      else f["notes"] = "NULL"
      if (f["sex"] != "NULL") f["sex"] = "'" f["sex"] "'"
      else f["sex"] = "NULL"
      error= db_sql("seq1","INSERT INTO sample (sample_no, alaID, sex, notes)"\
                    "VALUES (" f["sample_no"] ",'" DBQ[1,"id"] "'," f["sex"] \
                    "," f["notes"] ");")
      if (error) actiontext = "<span style=\"color:red;\">INSERT FAILURE</span>"
      else actiontext = "<span style=\"color:green;\">Sample " f["sample_no"] \
             " " f["barcode"] " " DBQ[1,"alaac"] " " \
             DBQ[1,"taxon"] " added</span>"
    }
    db_clear()
  }

  # Block 2: form

  print "<div id=\"input\">"
  print "<form action=\"do\"><p>"
  print "<input type=\"hidden\" name=\"preaction\" value=\"add\"/>"
  print "Sample: <input type=\"text\" name=\"sample_no\" "\
    "style=\"width:50px;\"/>"
  print "&#160;&#160;Barcode: <input type=\"text\" name=\"barcode\" "\
    "style=\"width:100px;\"/>"
  print "&#160;&#160;Sex: <select name=\"sex\">"\
    "<option value=\"NULL\" selected=\"1\"></option>"\
    "<option value=\"M\">M</option>"\
    "<option value=\"F\">F</option>"\
    "<option value=\"F\">ST</option>"\
    "</select>"
  print "&#160;&#160;Notes: <input type=\"text\" name=\"notes\" "\
    "style=\"width:300px;\"/>"
  print "&#160;&#160;&#160;<input type=\"submit\" value=\"Add\"/></p></form>"
  print "</div>"

  # Block 3: actions
  print "<div id=\"action\">"
  print actiontext
  print "</div>"

  # Main query
  db_query("seq1", "SELECT sample_no, barcode, notes, taxon, alaac, sex, guid " \
           "FROM sample, ala WHERE sample.alaID = ala.id ORDER BY sample_no;")

  # Block 4: taxon summary

  for (i = 1 ; i <= DBQr; i++) {
    split(DBQ[i, "taxon"],ts)
    taxsum[(ts[1] " " ts[2])]++
  }

  print "<div id=\"summary\">"
  print "<table>"
  print "<tr>"
  print "<th>Species</th>"
  print "<th>N samples</th>"
  print "</tr>"
  for (i in taxsum) {
    print "<tr>"
    print "<td><i>" i "</i></td>"
    print "<td>" taxsum[i] "</td>"
    print "</tr>"
  }
  print "</table>"
  print "</div>"

  # Block 5: main table

  print "<div id=\"main\">"
  print "<table>"
  print "<tr>"
  print "<th>Sample</th>"
  print "<th>Barcode</th>"
  print "<th>Taxon</th>"
  print "<th>ALAAC</th>"
  print "<th>GUID</th>"
  print "<th></th>"
  print "<th>Sex</th>"
  print "<th>Notes</th>"
  print "</tr>"

  # Table data
  for (i = 1 ; i <= DBQr; i++) {
    print "<tr>"
    print "<td>" DBQ[i, "sample_no"] "</td>"
    print "<td>" DBQ[i, "barcode"] "</td>"
    print "<td>" DBQ[i, "taxon"] "</td>"
    print "<td>" DBQ[i, "alaac"] "</td>"
    print "<td><a href=\"https://arctos.database.museum/guid/" DBQ[i, "guid"]\
      "\" target=\"_blank\">" DBQ[i, "guid"] "</a></td>"
    print "<td> <a href=\"do?page=seeall&amp;preaction=delete&amp;del=" \
      DBQ[i, "sample_no"] "\" onclick=\"return confirm('Confirm sample " \
      DBQ[i, "sample_no"] " deletion')\">delete</a> </td>"
    print "<td>" DBQ[i, "sex"] "</td>"
    print "<td>" substr(DBQ[i, "notes"],1,100) "</td>"
    print "</tr>"
  }
  print "</table>"
  print "</div>"

  db_clear()

  footer()

}


function header(title) {
  
  # Use html5
  print "Content-type: text/html\n"
  print "<!DOCTYPE html>"
  print "<html xmlns=\"http://www.w3.org/1999/xhtml\">"
  print "<head><title>" title "</title>"
  print "<meta http-equiv=\"Content-Type\" content=\"text/html; \
           charset=utf-8\" />"
  print "<link href=\"https://fonts.googleapis.com/css?family=Montserrat\" \
           rel=\"stylesheet\"/>"
  print "<link href=\"../img/akflora.ico\" rel=\"shortcut icon\" \
           type=\"image/x-icon\"/>"

  # CSS
  print "<style>"
  print "body { font-size: 14px; width: 1200px;	font-family: 'Montserrat', \
         Verdana, Arial, Helvetica, sans-serif; padding: 20px; \
         position: absolute }"
  # print "select , option { font-size: 14px }"
  print "div { padding-top: 10px; padding-bottom: 10px;}"
  print "div #action { min-height: 30px; }"
  print "table { max-width: 1200px; border-collapse: collapse }"
  print "td, th { border: 1px solid black; padding: 3px }"
  print "a { color:#15358d; text-decoration:none; border-bottom-style:none }"
  print "a:visited { color:#9f1dbc }"
  print "a:hover {color:#15358d; border-bottom-style:solid; \
	     border-bottom-width:thin }"
  print "</style>"

  print "</head>\n<body><div style=\"padding-left:100px;\">"
}


function footer()
{
  print "</div></body>\n</html>";
}

